#!/bin/tcsh -f

if ($#argv < 4) then
    echo "usage: <minutes per job> <settings file> <training file> <test file>"
    echo "	[<iteration dir>+]"
    echo "where"
    echo "	[<iteration dir>+] defaults to the glob ./[0-9][0-9]"
    exit 1
endif

set minutes = $1
set settings = $2
set trainingFile = $3
set testFile = $4
if ($#argv == 4) then
    # assume user wants all iteration dirs in current directory
    set iterDirs = (./[0-9][0-9])
else
    set iterDirs = ($argv[5-$#argv])
endif

set trainingPrefix = $trainingFile:t:r

# make sure all files/directories actually exist (except for trainingFile,
# which is only used for its name)
if (! -f $settings) then
    echo "error: settings file $settings does not exist"
    exit 1
endif

foreach dir ($iterDirs)
    if (! -d $dir) then
	echo "error: iteration directory $dir does not exist"
	exit 1
    endif
    if (! -f $dir/$trainingPrefix.precomp.obj.gz) then
	echo -n "error: derived data file $dir/$trainingPrefix.precomp.obj.gz "
	echo "does not exist"
	exit 1
    endif
end

if (! -f $testFile) then
    echo "error: test file $testFile does not exist"
    exit 1
endif

# give settings absolute path, if necessary
set settingsDir = `dirname $settings`
set settings = `cd $settingsDir; echo $cwd`/$settings:t

# give test file absolute path, if necessary
set testDir = `dirname $testFile`
set testFile = `cd $testDir; echo $cwd`/$testFile:t

# give each iterDir absolute path, if necessary
set tmpIterDirs = ()
foreach dir ($iterDirs)
    set tmpIterDirs = ($tmpIterDirs `cd $dir; echo $cwd`)
end
set iterDirs = ($tmpIterDirs)


set thisDir = `dirname $0`
set scriptDir = `cd $thisDir ; echo $cwd`

foreach dir ($iterDirs)
    echo "c" | cmsubmit -p 20 -t $minutes $scriptDir/wrapper \
	$scriptDir/internal-server-run $settings \
	$dir/$trainingPrefix.precomp.obj.gz $testFile
end
