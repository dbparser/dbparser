#!/bin/tcsh -f

goto afterUsage
usage:
    echo "usage: <max. heap in megabytes> <settings> <derived data file> <input file>"
    exit 1

afterUsage:

if ($#argv < 3) then
   goto usage
endif

# figuring out our location
set dir = `dirname $0`
set scriptDir = `cd $dir  ; echo $cwd`
set parent = `cd $scriptDir/.. ; echo $cwd`

#set java = java
set java = (java -server)
set class = danbikel.parser.Parser
set maxHeap = $1
set minHeap = $maxHeap
set settingsFile = $2
set settings = "-Dparser.settingsFile=$settingsFile"
set jarFile = $parent/dbparser.jar
set classpath = $jarFile
if ($?CLASSPATH) then
    set classpath = $jarFile\:$CLASSPATH
endif
set derivedDataFile = $3
set inFile = $4

set command = ($java -Xms$minHeap\m -Xmx$maxHeap\m -cp $classpath $settings \
	       $class -is $derivedDataFile -sa $inFile)

echo ""
echo Executing command
echo "\t"$command
echo ""

$command
