#!/bin/tcsh -f

goto afterUsage
usage:
    echo "usage: <max. heap size in megabytes> <settings> <parse tree input file>"
    exit 1

afterUsage:

if ($#argv < 3) then
   goto usage
endif

# figuring out our location
set dir = `dirname $0`
set scriptDir = `cd $dir  ; echo $cwd`
set parent = `cd $scriptDir/.. ; echo $cwd`

#set java = java
set java = (java -server)
set class = danbikel.parser.Trainer
set maxHeap = $1
set minHeap = $maxHeap
set settingsFile = $2
set settings = "-Dparser.settingsFile=$settingsFile"
set jarFile = $parent/dbparser.jar
set classpath = $jarFile
if ($?CLASSPATH) then
    set classpath = $jarFile\:$CLASSPATH
endif
set inFile = $3
set oFile = $inFile:r.observed.gz
set odFile = $inFile:r.obj.gz

set command = ($java -Xms$minHeap\m -Xmx$maxHeap\m -cp $classpath $settings \
	       $class -i $inFile -o $oFile -od $odFile)

echo ""
echo Executing command
echo "\t"$command
echo ""

$command
